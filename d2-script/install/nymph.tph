/*
nymph script
*/


INCLUDE ~%install%/nymphprep.tph~

//set script for nymph (if changed by a mod)
COPY_EXISTING ~NYMPHSU.CRE~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
    WRITE_ASCII 0x248 NYMPH #8
  END
BUT_ONLY

//speed bonus
COPY ~%nymph%/nymphspeed.SPL~  ~override/d2cwfey.SPL~
  PATCH_IF (is_ee > 0) BEGIN
    LPF d2_remove_effects_on_cast END
  END


//
//clone and edit projectile (classic BG2 only)
ACTION_IF (is_ee = 0) BEGIN
  COPY_EXISTING - ~SPPR105.spl~  ~~
    READ_SHORT 0x98 entangle_proj
  LOOKUP_IDS_SYMBOL_OF_INT oldproj ~projectl~ (entangle_proj - 1)    // number is 1 less in IDS file

  ACTION_IF (FILE_EXISTS_IN_GAME ~%oldproj%.PRO~) BEGIN
    COPY_EXISTING - ~%oldproj%.PRO~  ~inlined/d2cwbs1.PRO~
      WRITE_SHORT 0x200 THIS | BIT6    // affect enemies only (relative to caster)
    IF_EXISTS

    ADD_PROJECTILE ~inlined/d2cwbs1.PRO~    // set projectile in SPL to variable "d2cwbs1"
    OUTER_SET entangle_proj = d2cwbs1
  END
END


//
//clone entangle (party friendly)
COPY_EXISTING ~SPPR105.spl~  ~override/d2cwbs1.SPL~
  PATCH_IF (is_ee > 0) BEGIN
    LPF update_op324_restrictions END
    LPF d2_safe_spell INT_VAR neutrals=0 block_opcode=154 duration=5 END    // add op318 (EEs)
  END ELSE BEGIN
    LPF ALTER_SPELL_HEADER INT_VAR projectile=entangle_proj END    // or set new projectile (classic)
  END

OUTER_TEXT_SPRINT nymph_entangle ~"D2CWBS1"~

//CLERIC_SUMMON_INSECTS (replace CLERIC_MISCAST_MAGIC in scripts)


//
//install scripts
COPY - ~%nymph%/nymphcore.BAF~  ~inlined/d2cwfey.BAF~
  EVALUATE_BUFFER
  LPF ddoor_check END
COPY - ~%nymph%/nymphalt.BAF~   ~inlined/d2cwfey1.BAF~
  EVALUATE_BUFFER
  LPF ddoor_check END

COMPILE ~inlined/d2cwfey.BAF~
COPY_EXISTING ~d2cwfey.BCS~ ~override/NYMPH.BCS~

COPY - ~inlined/d2cwfey1.BAF~ ~inlined/d2cwfey2.BAF~
  REPLACE_TEXTUALLY ~Player1~ ~Player2~
  REPLACE_TEXTUALLY ~Player0~ ~Player1~
  REPLACE_TEXTUALLY ~d2cwfey2~ ~d2cwfey3~
COPY - ~inlined/d2cwfey1.BAF~ ~inlined/d2cwfey3.BAF~
  REPLACE_TEXTUALLY ~Player1~ ~Player3~
  REPLACE_TEXTUALLY ~Player0~ ~Player1~
  REPLACE_TEXTUALLY ~d2cwfey2~ ~d2cwfey4~
COPY - ~inlined/d2cwfey1.BAF~ ~inlined/d2cwfey4.BAF~
  REPLACE_TEXTUALLY ~Player1~ ~Player4~
  REPLACE_TEXTUALLY ~Player0~ ~Player1~
  REPLACE_TEXTUALLY ~d2cwfey2~ ~d2cwfey5~
COPY - ~inlined/d2cwfey1.BAF~ ~inlined/d2cwfey5.BAF~
  REPLACE_TEXTUALLY ~Player1~ ~Player5~
  REPLACE_TEXTUALLY ~Player0~ ~Player1~
  REPLACE_TEXTUALLY ~d2cwfey2~ ~d2cwfey6~
COPY - ~inlined/d2cwfey1.BAF~ ~inlined/d2cwfey6.BAF~
  REPLACE_TEXTUALLY ~Player1~ ~Player6~
  REPLACE_TEXTUALLY ~Player0~ ~Player1~
  REPLACE_TEXTUALLY ~d2cwfey2~ ~d2cwfey1~
COPY - ~inlined/d2cwfey1.BAF~ ~inlined/d2cwfey1.BAF~
  REPLACE_TEXTUALLY ~Player0~ ~Player1~

COMPILE ~inlined/d2cwfey1.BAF~
COMPILE ~inlined/d2cwfey2.BAF~
COMPILE ~inlined/d2cwfey3.BAF~
COMPILE ~inlined/d2cwfey4.BAF~
COMPILE ~inlined/d2cwfey5.BAF~
COMPILE ~inlined/d2cwfey6.BAF~


//
//