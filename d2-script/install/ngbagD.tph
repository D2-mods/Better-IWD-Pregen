// Bottomless, and Gold is exchanged when adding/removing items (experimental)

////////////////////////////////////////////////////////////

ACTION_IF (GAME_IS ~bgee bg2ee eet~) BEGIN
  COPY ~%ngbag%/d2#ngbag.BAM~ ~override~
  COPY ~%ngbag%/d2hbee1.ITM~ ~override/d2#ngbag.ITM~
    SAY NAME1 @730 SAY UNIDENTIFIED_DESC @734
    SAY NAME2 @730
    WRITE_ASCII 0x3a ~d2#ngbag~ #8    // set inventory icon
	
  COPY ~%ngbag%/d2hbee1.STO~ ~override/d2#ngbag.STO~
    SAY 0xc @730  /* Bag of Holding */
    WRITE_LONG 0x10 THIS | BIT13    // make buy price ignore rep
    WRITE_LONG 0x14 180    // 180% markup when buying back items
    WRITE_LONG 0x18 65    // receive 65% of base price
    WRITE_SHORT 0x22 65535    // set storage to max
	
  ACTION_IF (GAME_IS ~bg2ee~) BEGIN
    EXTEND_BOTTOM ~AR0602.BCS~ ~%ngbag%/d2#ngbg2.BAF~
  END
  
  ACTION_IF (GAME_IS ~eet~) BEGIN
    COPY - ~%ngbag%/d2#ngbg2.BAF~ ~%workspace%/d2#ngbg2.BAF~
      REPLACE_TEXTUALLY ~InParty("Imoen")~ ~InParty("Imoen2")~
      REPLACE_TEXTUALLY ~"d2#ngbag","Imoen"~ ~"d2#ngbag","Imoen2"~
    EXTEND_BOTTOM ~AR0602.BCS~ ~%workspace%/d2#ngbg2.BAF~
  END
  
  ACTION_IF (GAME_INCLUDES ~sod~) BEGIN
    COPY_EXISTING ~bd0120.BCS~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY 
          ~GiveItemCreate("BDKEYR",Player1,0,0,0)~
          ~GiveItemCreate("BDKEYR",Player1,0,0,0) GiveItemCreate("d2#ngbag",Player1,0,0,0)~
      END
    BUT_ONLY
  END
  
  EXTEND_TOP ~BALDUR.BCS~ ~%ngbag%/d2#ngbag.BAF~
END

////////////////////////////////////////////////////////////

ACTION_IF (GAME_IS ~iwdee~) BEGIN
ACTION_IF !(MOD_IS_INSTALLED ~d2-script.tp2~ (ID_OF_LABEL ~d2-script.tp2~ ~D2-TWEAKS-NGITEMBAG~)) BEGIN

  COPY ~%ngbag%/d2#ngbag.BAM~ ~override~
  COPY ~%ngbag%/d2hbee0.ITM~ ~override/d2#ngbag.ITM~
    SAY NAME1 @720 SAY UNIDENTIFIED_DESC @721
    SAY NAME2 @730 SAY DESC @734
    WRITE_ASCII 0x3a ~d2#ngbag~ #8    // set inventory icon
	
  COPY ~%ngbag%/d2hbee0.STO~ ~override/d2#ngbag.STO~
    SAY 0xc @730  /* Bag of Holding */
    WRITE_LONG 0x10 THIS | BIT13    // make buy price ignore rep
    WRITE_LONG 0x14 180    // 180% markup when buying back items
    WRITE_LONG 0x18 65    // receive 65% of base price
    WRITE_SHORT 0x22 65535    // set storage to max
	
  EXTEND_TOP ~BALDUR.BCS~ ~%ngbag%/d2#ngbag.BAF~
  
END
END

////////////////////////////////////////////////////////////

//IWD games: Patch existing bag if added by another component

ACTION_IF (MOD_IS_INSTALLED ~d2-script.tp2~ (ID_OF_LABEL ~d2-script.tp2~ ~D2-TWEAKS-NGITEMBAG~)) BEGIN
  COPY_EXISTING ~d2#ngbag.STO.BAK~ ~override/d2#ngbag.STO~    // revert edits to store file
  COPY_EXISTING ~d2#ngbag.STO~ ~override~
    WRITE_LONG 0x10 THIS | BIT13    // make buy price ignore rep
    WRITE_LONG 0x14 180    // 180% markup when buying back items
    WRITE_LONG 0x18 65    // receive 65% of base price
    WRITE_SHORT 0x22 65535    // set storage to max
  BUT_ONLY
  
  COPY ~%ngbag%/d2#ngbag.BAM~ ~override~
  COPY_EXISTING ~d2#ngbag.ITM.BAK~ ~override/d2#ngbag.ITM~    // revert edits to item file
  COPY_EXISTING ~d2#ngbag.ITM~ ~override~
    SAY DESC @734
    WRITE_ASCII 0x3a ~d2#ngbag~ #8    // set inventory icon
  BUT_ONLY
END

////////////////////////////////////////////////////////////

PRINT @54    // print message to check readme for additional info