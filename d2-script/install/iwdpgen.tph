//IWD Pregen (iwdpgen)
//same as vanilla script, but with shaman abilities added


ACTION_IF (ee_game > 0) BEGIN

  OUTER_SET was_it_patched = 0


//Patch existing file (for IWDEE)
ACTION_IF (FILE_EXISTS_IN_GAME ~iwdpgen.BCS~) BEGIN

  COPY_EXISTING ~iwdpgen.BCS~  ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~Class(Myself,THIEF_ALL)~   ~CheckStatGT(Myself,0,TRAPS)~
      REPLACE_TEXTUALLY ~Class(Myself,MONK)~        ~CheckStatGT(Myself,0,DETECTILLUSIONS)~
      REPLACE_TEXTUALLY ~!ModalState(DETECTTRAPS)~  ~!ModalState(BATTLESONG) !ModalState(DETECTTRAPS)~
      REPLACE_TEXTUALLY ~!ModalState(STEALTH)~      ~!ModalState(STEALTH) !ModalState(SHAMANDANCE)~
      COUNT_REGEXP_INSTANCES ~SHAMANDANCE~ was_it_patched
    END
  BUT_ONLY

  COPY ~override/iwdpgen.BCS~  ~scripts/iwdpgen.BS~

END


//or make new file (for BGEE games)
ACTION_IF ((was_it_patched = 0) OR 
          !(FILE_EXISTS_IN_GAME ~iwdpgen.BCS~)
  ) BEGIN

<<<<<<<< .../temp/iwdpgen.BAF
IF
	ActionListEmpty()
	OR(2)
		CheckStatGT(Myself,0,TRAPS)
		CheckStatGT(Myself,0,DETECTILLUSIONS)
	!ModalState(TURNUNDEAD)
	!ModalState(BATTLESONG)
	!ModalState(DETECTTRAPS)
	!ModalState(STEALTH)
	!ModalState(SHAMANDANCE)
	!Range([EVILCUTOFF],30)
THEN
	RESPONSE #100
		FindTraps()
END

IF
	ActionListEmpty()
	!StateCheck(Myself,STATE_INVISIBLE)
	CheckStatLT(Myself,1,SANCTUARY)
	!ModalState(BATTLESONG)
	!ModalState(TURNUNDEAD)
	!ModalState(STEALTH)
	!ModalState(SHAMANDANCE)
	See([EVILCUTOFF])
	!InParty([EVILCUTOFF])
THEN
	RESPONSE #100
		AttackReevaluate([EVILCUTOFF],30)
END
>>>>>>>>

COMPILE ~.../temp/iwdpgen.BAF~
COPY ~override/iwdpgen.BCS~  ~scripts/iwdpgen.BS~

END


//Script text
OUTER_SET name = RESOLVE_STR_REF (@99100)
OUTER_SET desc = RESOLVE_STR_REF (@99101)

COPY_EXISTING ~SCRPDESC.2DA~  ~override~
  LPF 2da_find_row_delete STR_VAR row_name = ~iwdpgen~ RET row_number columns END
  INSERT_2DA_ROW row_number columns ~iwdpgen %name% %desc%~
  PRETTY_PRINT_2DA
BUT_ONLY


END


/*
*/