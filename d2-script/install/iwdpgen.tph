////////////////////////////////////////////////////////////

ACTION_IF (GAME_IS ~iwdee~) BEGIN

COPY - ~%MOD_FOLDER%/baf/d2bsee1.BAF~ ~%MOD_FOLDER%/temp/d2bsee.BAF~    // rename and compile script
COMPILE ~%MOD_FOLDER%/temp/d2bsee.BAF~
EXTEND_BOTTOM ~d2bsee.BCS~ ~%MOD_FOLDER%/game/d2#iwdee.BAF~    // game-specific blocks
EXTEND_BOTTOM ~d2bsee.BCS~ ~%MOD_FOLDER%/baf/d2bsee2.BAF~
EXTEND_BOTTOM ~d2bsee.BCS~ ~%MOD_FOLDER%/baf/d2bsee3.BAF~
COPY_EXISTING ~d2bsee.BCS~ ~scripts/iwdpgen.BS~    // copy to scripts folder

ACTION_IF (FILE_EXISTS_IN_GAME ~iwdpgen.BCS~) BEGIN
  COPY_EXISTING ~iwdpgen.BCS~ ~override/iwdpgen.BAK~    // backup iwdpgen.BCS
  PRINT @50
END

COPY_EXISTING ~d2bsee.BCS~ ~override/iwdpgen.BCS~    // replace iwdpgen.BCS

COPY_EXISTING ~SCRPDESC.2DA~ ~override~    // update text
  LPF 2da_find_row_delete STR_VAR row_name = ~iwdpgen~ RET row_number columns END
  INSERT_2DA_ROW %row_number% %columns% ~IWDPGEN @100 @101~
  REPLACE ~@100~ @100
  REPLACE ~@101~ @101
  PRETTY_PRINT_2DA
BUT_ONLY

END

////////////////////////////////////////////////////////////

ACTION_IF (GAME_IS ~bgee bg2ee eet~) BEGIN

COPY - ~%MOD_FOLDER%/baf/d2bsee1.BAF~ ~%MOD_FOLDER%/temp/d2bsee.BAF~
COMPILE ~%MOD_FOLDER%/temp/d2bsee.BAF~

ACTION_IF ((GAME_IS ~bgee~) AND (NOT GAME_INCLUDES ~sod~)) BEGIN
EXTEND_BOTTOM ~d2bsee.BCS~ ~%MOD_FOLDER%/game/d2#bgee.BAF~
EXTEND_BOTTOM ~d2bsee.BCS~ ~%MOD_FOLDER%/baf/d2bsee2.BAF~
EXTEND_BOTTOM ~d2bsee.BCS~ ~%MOD_FOLDER%/baf/d2bsee3.BAF~
COPY_EXISTING ~d2bsee.BCS~ ~scripts/iwdpgen.BS~
END

ACTION_IF ((GAME_IS ~bgee~) AND (GAME_INCLUDES ~sod~)) BEGIN
EXTEND_BOTTOM ~d2bsee.BCS~ ~%MOD_FOLDER%/game/d2#sod.BAF~
EXTEND_BOTTOM ~d2bsee.BCS~ ~%MOD_FOLDER%/baf/d2bsee2.BAF~
EXTEND_BOTTOM ~d2bsee.BCS~ ~%MOD_FOLDER%/baf/d2bsee3.BAF~
COPY_EXISTING ~d2bsee.BCS~ ~scripts/iwdpgen.BS~
END

ACTION_IF (GAME_IS ~bg2ee~) BEGIN
EXTEND_BOTTOM ~d2bsee.BCS~ ~%MOD_FOLDER%/game/d2#bg2ee.BAF~
EXTEND_BOTTOM ~d2bsee.BCS~ ~%MOD_FOLDER%/baf/d2bsee2.BAF~
EXTEND_BOTTOM ~d2bsee.BCS~ ~%MOD_FOLDER%/baf/d2bsee3.BAF~
COPY_EXISTING ~d2bsee.BCS~ ~scripts/iwdpgen.BS~
END

ACTION_IF (GAME_IS ~eet~) BEGIN
EXTEND_BOTTOM ~d2bsee.BCS~ ~%MOD_FOLDER%/game/d2#eet.BAF~
EXTEND_BOTTOM ~d2bsee.BCS~ ~%MOD_FOLDER%/baf/d2bsee2.BAF~
EXTEND_BOTTOM ~d2bsee.BCS~ ~%MOD_FOLDER%/baf/d2bsee3.BAF~
COPY_EXISTING ~d2bsee.BCS~ ~scripts/iwdpgen.BS~
END

APPEND ~SCRPDESC.2DA~    // add line to 2DA (with placeholder values)
  ~iwdpgen     @100       @101~

COPY_EXISTING ~SCRPDESC.2DA~ ~override~    // replace placeholders with actual strings
  REPLACE ~@100~ @100
  REPLACE ~@101~ @101
  PRETTY_PRINT_2DA
BUT_ONLY

END

////////////////////////////////////////////////////////////

ACTION_IF (GAME_IS ~iwd2~) BEGIN

COPY ~%MOD_FOLDER%/iwd2/attstyle.ids~ ~override~    // copy dummy file, prevents error message
COMPILE ~%MOD_FOLDER%/baf/d2bsid2.BAF~
COPY_EXISTING ~d2bsid2.BCS~ ~scripts/iwdpgen.BS~

APPEND ~SCRPDESC.2DA~
  ~iwdpgen     @100       @102~

COPY_EXISTING ~SCRPDESC.2DA~ ~override~
  REPLACE ~@100~ @100
  REPLACE ~@102~ @102
  PRETTY_PRINT_2DA
BUT_ONLY

END

////////////////////////////////////////////////////////////

ACTION_IF (GAME_IS ~bg2 tob tutu tutu_totsc iwd_in_bg2 ca~) BEGIN

COMPILE ~%MOD_FOLDER%/baf/d2bsbg2.BAF~
COPY_EXISTING ~d2bsbg2.BCS~ ~scripts/iwdpgen.BS~

APPEND ~SCRPDESC.2DA~
  ~iwdpgen     @100       @103~

COPY_EXISTING ~SCRPDESC.2DA~ ~override~
  REPLACE ~@100~ @100
  REPLACE ~@103~ @103
  PRETTY_PRINT_2DA
BUT_ONLY

//fixed TobEx DLL, from Insomniator
//http://www.shsforums.net/topic/61135-question-about-featurebug-change-to-the-noaction-script-action/#entry612382
ACTION_IF (MOD_IS_INSTALLED ~TobEx.tp2~ 100) BEGIN
  COPY ~%MOD_FOLDER%/tobex/TobEx.dll~ ~TobEx.dll~
END

END

////////////////////////////////////////////////////////////

ACTION_IF (GAME_IS ~iwd how totlm~) BEGIN

COMPILE ~%MOD_FOLDER%/baf/d2bsid1.BAF~
COPY_EXISTING ~d2bsid1.BCS~ ~scripts/iwdpgen.BS~

APPEND ~SCRPDESC.2DA~
  ~iwdpgen     @100       @104~

COPY_EXISTING ~SCRPDESC.2DA~ ~override~
  REPLACE ~@100~ @100
  REPLACE ~@104~ @104
  PRETTY_PRINT_2DA
BUT_ONLY

END

////////////////////////////////////////////////////////////

ACTION_IF (GAME_IS ~bg1 totsc~) BEGIN

COMPILE ~%MOD_FOLDER%/baf/d2bsbg1.BAF~
COPY_EXISTING ~d2bsbg1.BCS~ ~scripts/iwdpgen.BS~

APPEND ~SCRPDESC.2DA~
  ~iwdpgen     @100       @105~

COPY_EXISTING ~SCRPDESC.2DA~ ~override~
  REPLACE ~@100~ @100
  REPLACE ~@105~ @105
  PRETTY_PRINT_2DA
BUT_ONLY

END

////////////////////////////////////////////////////////////




/*REMOVED from classic BG2 section, replaced with fixed DLL file

ACTION_IF (MOD_IS_INSTALLED ~bg2improvedgui.tp2~ 1200) BEGIN ABORT @55 END    // abort if Extended Text is installed (GUI mod)

//disable Expanded Actions hack (if ToBEx detected)
ACTION_IF (MOD_IS_INSTALLED ~TobEx.tp2~ 100) BEGIN
  COPY ~TobEx_ini/TobExCore.ini~ ~TobEx_ini/TobExCore.ini~
    REPLACE_TEXTUALLY ~Expanded Actions=1~ ~Expanded Actions=0~
  PRINT @51
END

*/