//d2scrp+
//Calls for help enabled


//make blank BCS file
<<<<<<<< .../temp/d2scrp+.BAF
>>>>>>>>
COMPILE ~.../temp/d2scrp+.BAF~


//change text color (EEs)
ACTION_IF (~%game%~ STR_EQ ~ee~) BEGIN
  LAF d2_color_string INT_VAR stringref=RESOLVE_STR_REF (@1001) STR_VAR color=blue RET newstring END
  OUTER_TEXT_SPRINT activate_cooldown ~%newstring%~
  LAF d2_color_string INT_VAR stringref=RESOLVE_STR_REF (@1003) STR_VAR color=blue RET newstring END
  OUTER_TEXT_SPRINT disable_cooldown ~%newstring%~
END


//edit #variables.BAF
COPY ~%blocks%/#variables.BAF~  ~%workspace%~
  EVALUATE_BUFFER


//create script
ACTION_FOR_EACH baf_to_add IN
  ~#variables~
  ~#invis~
  ~#passive~
  ~#assist~
  ~#game~
  ~#immune~
  ~#caster~
  ~#attack~
  ~#retarget~
  ~#end~
BEGIN

  ACTION_IF (FILE_EXISTS ~%workspace%/%baf_to_add%.BAF~) BEGIN
    EXTEND_BOTTOM ~d2scrp+.BCS~  ~%workspace%/%baf_to_add%.BAF~
  END ELSE
  ACTION_IF (FILE_EXISTS ~%blocks%/%baf_to_add%.BAF~) BEGIN
    EXTEND_BOTTOM ~d2scrp+.BCS~  ~%blocks%/%baf_to_add%.BAF~
  END

END

COPY_EXISTING ~d2scrp+.BCS~  ~scripts/d2scrp+.BS~    // copy to scripts folder


//Script text
OUTER_SET name = RESOLVE_STR_REF (@33100)
OUTER_SET desc = RESOLVE_STR_REF (@33101)

COPY_EXISTING ~SCRPDESC.2DA~  ~override~
  LPF 2da_find_row_delete STR_VAR row_name = ~d2scrp+~ RET row_number columns END
  INSERT_2DA_ROW row_number columns ~d2scrp+ %name% %desc%~
  PRETTY_PRINT_2DA
BUT_ONLY


/*
*/