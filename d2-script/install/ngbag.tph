/*
Give party a Bag of Holding at game start (classic and EEs)
This file is for bottomless, 50 items, or 100 items options.
*/


/*IWD2 bags:
each row counts as 1 item, nonstackables cannot stack, stackables stack up to ITM max
100 arrows (80 stack size) will be 1 row of 80 and 1 row of 20, and count as 2 items
5 long swords will be 5 rows of 1 long sword
*/

/*BG2 bags (also EEs and HoW/TotlM):
each item counts as 1 item, nonstackables can stack in a row, no limit to stack size
100 arrows (80 stack size) will be 1 row of 100, and count as 100 items
5 long swords will be 1 row with 5 long swords
*/


//prep
ACTION_IF (capacity = 1) BEGIN
  OUTER_SET capacity = 32767 
  ACTION_IF (GAME_IS ~iwd2~) BEGIN  OUTER_SET id2_desc = RESOLVE_STR_REF (@735)  END ELSE
  ACTION_IF (GAME_IS ~pstee~) BEGIN  OUTER_SET pst_desc = RESOLVE_STR_REF (@778731)  END ELSE
  BEGIN  OUTER_SET bg2_desc = RESOLVE_STR_REF (@731)  END
END

ACTION_IF (capacity = 2) BEGIN
  OUTER_SET capacity = 50
  ACTION_IF (GAME_IS ~iwd2~) BEGIN  OUTER_SET id2_desc = RESOLVE_STR_REF (@736)  END ELSE
  ACTION_IF (GAME_IS ~pstee~) BEGIN  OUTER_SET pst_desc = RESOLVE_STR_REF (@778732)  END ELSE
  BEGIN  OUTER_SET bg2_desc = RESOLVE_STR_REF (@732)  END
END

ACTION_IF (capacity = 3) BEGIN
  OUTER_SET capacity = 100
  ACTION_IF (GAME_IS ~iwd2~) BEGIN  OUTER_SET id2_desc = RESOLVE_STR_REF (@737)  END ELSE
  ACTION_IF (GAME_IS ~pstee~) BEGIN  OUTER_SET pst_desc = RESOLVE_STR_REF (@778733)  END ELSE
  BEGIN  OUTER_SET bg2_desc = RESOLVE_STR_REF (@733)  END
END


////////////////////////////////////////////////////////////

//bgee, bg2ee

ACTION_IF (GAME_IS ~bgee bg2ee eet~) BEGIN
  COPY ~%ngbag%/d2hbee1.ITM~ ~override/d2#ngbag.ITM~
    SAY NAME1 @730 WRITE_LONG 0x50 bg2_desc
    SAY NAME2 @730
	
  COPY ~%ngbag%/d2hbee1.STO~ ~override/d2#ngbag.STO~
    SAY 0xc @730  /* Bag of Holding */
    WRITE_SHORT 0x22 capacity
	
  ACTION_IF !(FILE_EXISTS_IN_GAME ~ibag04.BAM~) BEGIN
    COPY ~%ngbag%/icon-ee1.BAM~ ~override/ibag04.BAM~
  END
  
  ACTION_IF (GAME_INCLUDES ~bg2~) BEGIN
    EXTEND_BOTTOM ~AR0602.BCS~ ~%ngbag%/ngbag-imoen.BAF~
  END
  
  ACTION_IF (GAME_INCLUDES ~sod~) BEGIN
    EXTEND_BOTTOM ~bd0120.BCS~  ~%ngbag%/ngbag-sodscript.BAF~
  END

  ACTION_FOR_EACH script IN ~BPHUB~ ~ohbhub~ BEGIN
    ACTION_IF (FILE_EXISTS_IN_GAME ~%script%.BCS~) BEGIN
      EXTEND_BOTTOM ~%script%.BCS~  ~%ngbag%/ngbag-bpscript.BAF~
    END
  END
  
  ACTION_FOR_EACH script IN ~BALDUR~ ~bdbaldur~ BEGIN
    ACTION_IF (FILE_EXISTS_IN_GAME ~%script%.BCS~) BEGIN
      EXTEND_TOP ~%script%.BCS~  ~%ngbag%/ngbag-script.BAF~
    END
  END

END

////////////////////////////////////////////////////////////

//iwdee

ACTION_IF (GAME_IS ~iwdee~) BEGIN
ACTION_IF !(MOD_IS_INSTALLED ~d2-script.tp2~ (ID_OF_LABEL ~d2-script.tp2~ ~D2-TWEAKS-NGITEMBAG~)) BEGIN

  COPY ~%ngbag%/d2hbee0.ITM~ ~override/d2#ngbag.ITM~
    SAY NAME1 @720 SAY UNIDENTIFIED_DESC @721
    SAY NAME2 @730 WRITE_LONG 0x54 bg2_desc
	
  COPY ~%ngbag%/d2hbee0.STO~ ~override/d2#ngbag.STO~
    SAY 0xc @730  /* Bag of Holding */
    WRITE_SHORT 0x22 capacity
	
  ACTION_IF !(FILE_EXISTS_IN_GAME ~ibaghold.BAM~) BEGIN
    COPY ~%ngbag%/icon-ee0.BAM~ ~override/ibaghold.BAM~
  END
  
  ACTION_FOR_EACH script IN ~BALDUR~ ~BALDUR25~ ~bdbaldur~ BEGIN
    ACTION_IF (FILE_EXISTS_IN_GAME ~%script%.BCS~) BEGIN
      EXTEND_TOP ~%script%.BCS~  ~%ngbag%/ngbag-script.BAF~
    END
  END
  
END
END

////////////////////////////////////////////////////////////

//iwd2

ACTION_IF (GAME_IS ~iwd2~) BEGIN
ACTION_IF !(MOD_IS_INSTALLED ~d2-script.tp2~ (ID_OF_LABEL ~d2-script.tp2~ ~D2-TWEAKS-NGITEMBAG~)) BEGIN

  COPY ~%ngbag%/d2hbid2.ITM~ ~override/d2#ngbag.ITM~
    SAY NAME1 @720 SAY UNIDENTIFIED_DESC @722
    SAY NAME2 @730 WRITE_LONG 0x54 id2_desc
	
  COPY ~%ngbag%/d2hbid2.STO~ ~override/d2#ngbag.STO~
    SAY 0xc @730  /* Bag of Holding */
    WRITE_SHORT 0x9c capacity
  
  ACTION_IF !(FILE_EXISTS_IN_GAME ~ibaghold.BAM~) BEGIN
    COPY ~%ngbag%/icon-id2.BAM~ ~override/ibaghold.BAM~
  END
  
  EXTEND_TOP ~BALDUR.BCS~ ~%ngbag%/ngbag-script.BAF~
  
END
END

////////////////////////////////////////////////////////////

//iwd1 (classic)

ACTION_IF (GAME_IS ~how totlm~) BEGIN
ACTION_IF !(MOD_IS_INSTALLED ~d2-script.tp2~ (ID_OF_LABEL ~d2-script.tp2~ ~D2-TWEAKS-NGITEMBAG~)) BEGIN

  COPY ~%ngbag%/d2hbid1.ITM~ ~override/d2#ngbag.ITM~
    SAY NAME1 @720 SAY UNIDENTIFIED_DESC @721
    SAY NAME2 @730 WRITE_LONG 0x54 bg2_desc
	
  COPY ~%ngbag%/d2hbid1.STO~ ~override/d2#ngbag.STO~
    SAY 0xc @730  /* Bag of Holding */
    WRITE_SHORT 0x9c capacity
  
  ACTION_IF !(FILE_EXISTS_IN_GAME ~ibaghold.BAM~) BEGIN
    COPY ~%ngbag%/icon-id1.BAM~ ~override/ibaghold.BAM~
  END
  
  EXTEND_TOP ~BALDUR.BCS~ ~%ngbag%/ngbag-script.BAF~
  
END
END

////////////////////////////////////////////////////////////

//IWD-in-BG2 mod

ACTION_IF (GAME_IS ~iwd_in_bg2~) BEGIN
ACTION_IF !(MOD_IS_INSTALLED ~d2-script.tp2~ (ID_OF_LABEL ~d2-script.tp2~ ~D2-TWEAKS-NGITEMBAG~)) BEGIN

  COPY ~%ngbag%/d2hbee0.ITM~ ~override/d2#ngbag.ITM~
    SAY NAME1 @720 SAY UNIDENTIFIED_DESC @721
    SAY NAME2 @730 WRITE_LONG 0x54 bg2_desc
	
  COPY ~%ngbag%/d2hbee0.STO~ ~override/d2#ngbag.STO~
    SAY 0xc @730  /* Bag of Holding */
    WRITE_SHORT 0x22 capacity
  
  ACTION_IF !(FILE_EXISTS_IN_GAME ~ibaghold.BAM~) BEGIN
    COPY ~%ngbag%/icon-id1.BAM~ ~override/ibaghold.BAM~
  END
  
  EXTEND_TOP ~BALDUR.BCS~ ~%ngbag%/ngbag-script.BAF~
  
END
END

////////////////////////////////////////////////////////////

//bg2 engine (classic)

ACTION_IF (GAME_IS ~bg2 tob tutu tutu_totsc ca~) BEGIN
  COPY ~%ngbag%/d2hbbg2.ITM~ ~override/d2#ngbag.ITM~
    SAY NAME1 @730 WRITE_LONG 0x50 bg2_desc
    SAY NAME2 @730
	
  COPY ~%ngbag%/d2hbbg2.STO~ ~override/d2#ngbag.STO~
    SAY 0xc @730  /* Bag of Holding */
    WRITE_SHORT 0x22 capacity
  
  ACTION_IF !(FILE_EXISTS_IN_GAME ~ibag04.BAM~) BEGIN
    COPY ~%ngbag%/icon-bg2.BAM~ ~override/ibag04.BAM~
  END
  
  ACTION_IF (GAME_INCLUDES ~bg2~) BEGIN
    EXTEND_BOTTOM ~AR0602.BCS~ ~%ngbag%/ngbag-imoen.BAF~
  END
  
  ACTION_IF (FILE_EXISTS ~override/lhMIS0a.itm~) BEGIN
    EXTEND_BOTTOM ~BALDUR.BCS~ ~%ngbag%/ngbag-epicadv.BAF~
  END
  
  ACTION_IF (GAME_IS ~ca~) BEGIN
    EXTEND_BOTTOM ~TC0009.BCS~ ~%ngbag%/ngbag-ca.BAF~
  END
  
  EXTEND_TOP ~BALDUR.BCS~ ~%ngbag%/ngbag-script.BAF~
END

////////////////////////////////////////////////////////////

//pstee

ACTION_IF (GAME_IS ~pstee~) BEGIN
  OUTER_TEXT_SPRINT icon  ~II130~
  COPY ~%ngbag%/pstbag.ITM~ ~override/d2#ngbag.ITM~
    SAY NAME1 @730
    SAY NAME2 @730 WRITE_LONG 0x54 pst_desc
    WRITE_ASCII 0x3a ~%icon%~ #8
    WRITE_ASCII 0x44 WMTPOT #8       // ground bottle icon (match Argent77's bag mod)
    WRITE_ASCII 0x10 INT_12O1 #8     // drop sound
    WRITE_ASCII 0x58 INT_12O #8      // pick up sound

  COPY ~%ngbag%/pstbag.STO~ ~override/d2#ngbag.STO~
    SAY 0xc @730
    WRITE_SHORT 0x22 capacity

  ACTION_IF !(FILE_EXISTS_IN_GAME ~%icon%.BAM~) BEGIN
    COPY ~%ngbag%/pstbag.BAM~ ~override/%icon%.BAM~
  END

  EXTEND_TOP ~BALDUR.BCS~ ~%ngbag%/ngbag-script.BAF~
END

////////////////////////////////////////////////////////////

//IWD games - Patches existing bag if already added

ACTION_IF (MOD_IS_INSTALLED ~d2-script.tp2~ (ID_OF_LABEL ~d2-script.tp2~ ~D2-TWEAKS-NGITEMBAG~)) BEGIN  

  COPY_EXISTING ~d2#ngbak.STO~ ~override/d2#ngbag.STO~    // revert edits to store file
    PATCH_IF (GAME_IS ~iwd2 how totlm~) BEGIN
      WRITE_SHORT 0x9c capacity
    END

    PATCH_IF (GAME_IS ~iwdee iwd_in_bg2 ca~) BEGIN
      WRITE_SHORT 0x22 capacity
    END
  // end COPY

  COPY_EXISTING ~d2#ngbak.ITM~ ~override/d2#ngbag.ITM~    // revert edits to item file
    PATCH_IF (GAME_IS ~iwd2~) BEGIN
      WRITE_LONG 0x54 id2_desc
    END ELSE BEGIN
      WRITE_LONG 0x54 bg2_desc
    END
  // end COPY

END

////////////////////////////////////////////////////////////