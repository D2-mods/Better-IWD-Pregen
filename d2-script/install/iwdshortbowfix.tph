//
//


//Patch visuals for shortbows (IWD:EE)
ACTION_IF (GAME_IS ~iwdee~) BEGIN

  COPY_EXISTING_REGEXP GLOB ~.*\.itm~ override
    PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
    PATCH_IF !(~%SOURCE_RES%~ STR_EQ ~GODBOW~) BEGIN    // skip if Godbow
    
      READ_BYTE 0x31 prof                 // checks weapon proficiency
      PATCH_IF (prof = 105) BEGIN         // shortbow prof (note: some mods combine longbows into this)
        READ_SHORT 0x80 range             // checks launcher range
        PATCH_IF (range = 75) BEGIN       // patch if 75 range (standard for shortbows)
          WRITE_ASCII 0x22 ~BS~ #2        // BS = shortbow appearance
          WRITE_ASCII 0x44 ~GBOW02~ #8    // change ground icon
        END
      END
  	
      PATCH_IF (!(prof = 105) OR !(range = 75)) BEGIN
        READ_STRREF 0x8 weapon            // checks unidentified name
        PATCH_IF ((~%weapon%~ STR_EQ ~Shortbow~) OR
                  (~%weapon%~ STR_EQ ~Short Bow~)
        ) BEGIN
          WRITE_ASCII 0x22 ~BS~ #2        // BS = shortbow appearance
          WRITE_ASCII 0x44 ~GBOW02~ #8    // change ground icon
        END
      END
  
    END
    END
  BUT_ONLY

END


//
//Patch visuals for scimitars (IWD-in-BG2)
ACTION_IF (GAME_IS ~iwd_in_bg2~) BEGIN

  COPY_EXISTING_REGEXP GLOB ~.*\.itm~ override
    PATCH_IF (SOURCE_SIZE > 0x71) BEGIN

      READ_BYTE 0x31 prof                  // checks weapon proficiency
      PATCH_IF (prof = 95) BEGIN           // scimitar prof
        READ_STRREF 0x8 weapon             // checks unidentified name
  	  
        PATCH_IF (~%weapon%~ STR_EQ ~Scimitar~) BEGIN
          WRITE_ASCII 0x22 ~SC~ #2    // SC = scimitar appearance
        END
  	  
        PATCH_IF (~%weapon%~ STR_EQ ~Wakizashi~) BEGIN
          WRITE_ASCII 0x22 ~SS~ #2    // SS = short sword appearance
        END
  	  
        PATCH_IF (~%weapon%~ STR_EQ ~Ninja-To~) BEGIN
          WRITE_ASCII 0x22 ~S1~ #2    // S1 = long sword appearance
        END
      END

    END
  BUT_ONLY

END


/*
*/