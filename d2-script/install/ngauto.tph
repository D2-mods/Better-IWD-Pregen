//auto-assign script for new characters
//


////////////////////////////////////////////////////////////

ACTION_IF (GAME_IS ~iwd2 iwd how totlm~) BEGIN


//classic IWD games
<<<<<<<< .../temp/ngauto
IF
	ActionListEmpty()
	!Global("d2ngauto","LOCALS",1)
	InParty(Myself)
THEN
	RESPONSE #100
		ChangeAIScript("%ngauto%",3)
		SetGlobal("d2ngauto","LOCALS",1)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/ngauto~  ~inlined/ngauto.BAF~
  EVALUATE_BUFFER


END
ELSE BEGIN


ACTION_IF !(GAME_IS ~bg1 totsc~) BEGIN

//EEs + BG2 engine
<<<<<<<< .../temp/ngauto
IF
	ActionListEmpty()
	!Global("d2ngauto","LOCALS",1)
	InParty(Myself)
THEN
	RESPONSE #100
		ChangeAIScript("%ngauto%",4)
		SetGlobal("d2ngauto","LOCALS",1)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/ngauto~  ~inlined/ngauto.BAF~
  EVALUATE_BUFFER


END
ELSE BEGIN


//classic BG1 (needs to use GLOBAL variable)
<<<<<<<< .../temp/ngauto
IF
	!Global("d2ngautoPlayer1","GLOBAL",1)
	InParty(Player1)
THEN
	RESPONSE #100
		ActionOverride(Player1,ChangeAIScript("%ngauto%",4))
		SetGlobal("d2ngautoPlayer1","GLOBAL",1)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/ngauto~  ~inlined/ngauto.BAF~
  EVALUATE_BUFFER
COPY - ~inlined/ngauto.BAF~  ~inlined/ngauto2.BAF~
  REPLACE_TEXTUALLY ~Player1~  ~Player2~
COPY - ~inlined/ngauto.BAF~  ~inlined/ngauto3.BAF~
  REPLACE_TEXTUALLY ~Player1~  ~Player3~
COPY - ~inlined/ngauto.BAF~  ~inlined/ngauto4.BAF~
  REPLACE_TEXTUALLY ~Player1~  ~Player4~
COPY - ~inlined/ngauto.BAF~  ~inlined/ngauto5.BAF~
  REPLACE_TEXTUALLY ~Player1~  ~Player5~
COPY - ~inlined/ngauto.BAF~  ~inlined/ngauto6.BAF~
  REPLACE_TEXTUALLY ~Player1~  ~Player6~


END
END

////////////////////////////////////////////////////////////

//fix SCRLEV.IDS for iwd1/iwd2 (symbolic names matched with wrong script offsets)
ACTION_IF (GAME_IS ~iwd2~) BEGIN
  COPY ~%iwd2%/SCRLEV2.IDS~ ~override/SCRLEV.IDS~
END

ACTION_IF (GAME_IS ~iwd how totlm~) BEGIN
  COPY ~%iwd2%/SCRLEV1.IDS~ ~override/SCRLEV.IDS~
END


//add script block to DPLAYER files
ACTION_IF !(GAME_IS ~bg1 totsc~) BEGIN

  ACTION_IF (FILE_EXISTS_IN_GAME ~DPLAYER2.BCS~) BEGIN
    EXTEND_TOP ~DPLAYER2.BCS~ ~inlined/ngauto.BAF~
  END
  
  ACTION_IF (FILE_EXISTS_IN_GAME ~DPLAYER3.BCS~) BEGIN
    EXTEND_TOP ~DPLAYER3.BCS~ ~inlined/ngauto.BAF~
  END

END
ELSE BEGIN    // for classic BG1

  ACTION_FOR_EACH script IN
    ~DPLAYER2~
    ~DPLAYER3~
  BEGIN
    EXTEND_TOP ~%script%.BCS~  ~inlined/ngauto.BAF~
    EXTEND_TOP ~%script%.BCS~  ~inlined/ngauto2.BAF~
    EXTEND_TOP ~%script%.BCS~  ~inlined/ngauto3.BAF~
    EXTEND_TOP ~%script%.BCS~  ~inlined/ngauto4.BAF~
    EXTEND_TOP ~%script%.BCS~  ~inlined/ngauto5.BAF~
    EXTEND_TOP ~%script%.BCS~  ~inlined/ngauto6.BAF~
  END

END

////////////////////////////////////////////////////////////

ACTION_IF (GAME_IS ~bgee bg2ee eet~) BEGIN

  ACTION_FOR_EACH area IN
    ~AR2600~  // BG1
    ~bd0120~  // SoD
    ~AR0602~  // BG2
    ~AR4000~  // ToB
    ~BG2600~  // EET
  BEGIN

    COPY_EXISTING ~%area%.BCS~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~BDDEFAI~ ~%ngauto%~    // default script for main PC
      END
    IF_EXISTS BUT_ONLY
  END


  COPY_EXISTING ~partyai.2DA~ ~override~
    COUNT_2DA_ROWS 1 row_count
    FOR (row = 3; row < row_count; ++row) BEGIN
      READ_2DA_ENTRY row 1 1 ~match~
      PATCH_IF !(~%match%~ STRING_EQUAL_CASE ~%ngauto%~) BEGIN
        SET_2DA_ENTRY_LATER ~d2partyai_edits~ row 1 ~%ngauto%~
      END
    END
    
    SET_2DA_ENTRIES_NOW ~d2partyai_edits~ 1
    PRETTY_PRINT_2DA
  IF_EXISTS BUT_ONLY
  
END

////////////////////////////////////////////////////////////