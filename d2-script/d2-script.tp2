BACKUP ~d2-script\backup~
AUTHOR ~Dan_P~
VERSION ~2.0~

LANGUAGE ~English~ 
         ~english~ 
		 ~d2-script\tra\english\setup.tra~


//Better IWD Pregen (BGEE, BG2EE, EET, IWDEE, IWD2)

BEGIN @1
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet iwdee iwd2~ @2
LABEL ~D2-SCRIPT-IWDPGEN~

//////////////////////////////////////////////////
ACTION_IF (GAME_IS ~iwdee~) BEGIN

ACTION_IF (FILE_EXISTS ~override\iwdpgen.BCS~)
BEGIN WARN @99 END    // iwdpgen.BCS detected in override

COMPILE ~%MOD_FOLDER%\baf\d2bsee0.BAF~    // create BCS file
COPY_EXISTING ~d2bsee0.BCS~ ~scripts\iwdpgen.BS~    // copy to scripts folder

COPY_EXISTING ~SCRPDESC.2DA~ ~override~    // update text
  REPLACE ~40838~ @10
  REPLACE ~40839~ @11
  PRETTY_PRINT_2DA
BUT_ONLY

END
//////////////////////////////////////////////////
ACTION_IF (GAME_IS ~bgee bg2ee eet~) BEGIN

COMPILE ~%MOD_FOLDER%\baf\d2bsee1.BAF~
COPY_EXISTING ~d2bsee1.BCS~ ~scripts\iwdpgen.BS~

APPEND ~SCRPDESC.2DA~    // add line to 2DA (with placeholder values)
  ~iwdpgen     @10       @11~

COPY_EXISTING ~SCRPDESC.2DA~ ~override~    // replace placeholders with actual strings
  REPLACE ~@10~ @10
  REPLACE ~@11~ @11
  PRETTY_PRINT_2DA
BUT_ONLY

END
//////////////////////////////////////////////////
ACTION_IF (GAME_IS ~iwd2~) BEGIN

COPY ~%MOD_FOLDER%\dummy~ ~override~    // copy dummy file, prevents error message
COMPILE ~%MOD_FOLDER%\baf\d2bsid2.BAF~
COPY_EXISTING ~d2bsid2.BCS~ ~scripts\iwdpgen.BS~

APPEND ~SCRPDESC.2DA~
  ~iwdpgen     @10       @12~

COPY_EXISTING ~SCRPDESC.2DA~ ~override~
  REPLACE ~@10~ @10
  REPLACE ~@12~ @12
  PRETTY_PRINT_2DA
BUT_ONLY

END
//////////////////////////////////////////////////


//Better AI for Call Woodland Beings (BGEE, BG2EE, EET, IWDEE)

BEGIN @3
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee iwdee eet~ @4
LABEL ~D2-SCRIPT-NYMPH~

ACTION_IF (GAME_IS ~bgee bg2ee eet~) BEGIN
COMPILE ~%MOD_FOLDER%\nymph\d2cwb1.BAF~
COMPILE ~%MOD_FOLDER%\nymph\d2cwb1a.BAF~
COMPILE ~%MOD_FOLDER%\nymph\d2cwb1b.BAF~
COMPILE ~%MOD_FOLDER%\nymph\d2cwb1c.BAF~
COMPILE ~%MOD_FOLDER%\nymph\d2cwb1d.BAF~
COMPILE ~%MOD_FOLDER%\nymph\d2cwb1e.BAF~
COMPILE ~%MOD_FOLDER%\nymph\d2cwb1f.BAF~
COPY_EXISTING ~d2cwb1.BCS~ ~override\NYMPH.BCS~
END

ACTION_IF (GAME_IS ~iwdee~) BEGIN
COMPILE ~%MOD_FOLDER%\nymph\d2cwb0.BAF~
COMPILE ~%MOD_FOLDER%\nymph\d2cwb0a.BAF~
COMPILE ~%MOD_FOLDER%\nymph\d2cwb0b.BAF~
COMPILE ~%MOD_FOLDER%\nymph\d2cwb0c.BAF~
COMPILE ~%MOD_FOLDER%\nymph\d2cwb0d.BAF~
COMPILE ~%MOD_FOLDER%\nymph\d2cwb0e.BAF~
COMPILE ~%MOD_FOLDER%\nymph\d2cwb0f.BAF~
COPY_EXISTING ~d2cwb0.BCS~ ~override\NYMPH.BCS~
END

COPY_EXISTING ~NYMPHSU.CRE~ ~override~    // set script for nymph (if another mod changed it)
  PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
  WRITE_ASCII 0x248 NYMPH #8
  END
BUT_ONLY